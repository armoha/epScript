%include {
#include <stdio.h>
#include <assert.h>
#include <string>
#include <iostream>
#include <sstream>
#include "epparser.h"
#include "pygen.h"
#include "tokenAdapter.h"
#include "parserUtilities.h"
}

%syntax_error {
    errorOccured = true;
}

////////

%extra_argument { PyGenerator* pGen }
%token_type {Token*}
%destructor nt { delete $$; }

// Precedence table

%left COMMA.
%left ASSIGN.

%left LOR.
%left LAND.
%right LNOT.
%right DOUBLE_LNOT.  // Faster optimization for double lnot

%nonassoc EQ LE LT GE GT NE.

%left BITOR.
%left BITXOR.
%left BITAND.
%left LSHIFT RSHIFT.
%left PLUS MINUS.
%left DIVIDE MULTIPLY MOD.
%right UNARY BITNOT.
%nonassoc NUMBER NAME.
%left FUNCCALL.

%nonassoc IFX.
%nonassoc IFNOT.
%nonassoc ELSE.


program ::= chunks.

// Common expressions
numList(A) ::= NUMBER(B). { A = B; }
numList(A) ::= numList(B) COMMA NUMBER(C). {
    A = new Token(B->data + ", " + C->data, C);
    delete B; delete C;
}
nameList(A) ::= NAME(B) COMMA NAME(C). { A = new Token((B->data) + ", " + (C->data), C); delete B; delete C; }
nameList(A) ::= nameList(B) COMMA NAME(C). { A = new Token((B->data) + ", " + (C->data), C); delete B; delete C; }
exprList(A) ::= funcexpr(B) LSQBRACKET LSQBRACKET numList(C) RSQBRACKET RSQBRACKET. {
    A = genTemp(C);
    (*pGen) << A->data << " = SelectReturns(" << B->data << ", [" << C->data << "])" << std::endl;
    delete B; delete C;
}

exprList(A) ::= expr(B). [COMMA] { A = B; }
exprList(A) ::= exprList(B) COMMA expr(C). { A = new Token((B->data) + ", " + (C->data), C); delete B; delete C; }
emptyableExprList(A) ::= exprList(B). [COMMA] { A = B; }
emptyableExprList(A) ::= . [COMMA] { A = new Token(TOKEN_TEMP, "", -1); }

// Related to expressions
expr(A) ::= NUMBER(B). { A = B; }
expr(A) ::= NAME(B). {
    if(pGen->undefined(B->data)) {
        throw_error(B->line, 103, ("Undeclared variable \'" + (B->data) + "\'"));
        pGen->namedef(B->data);
    }
    A = B;
}

// Function calls
funcexpr(out) ::= NAME(name) LPAREN emptyableExprList(exprs) RPAREN. {
    // Preprocess name
    if(name->data[0] == '_' || ('A' <= name->data[0] && name->data[0] <= 'Z'));  // Name starts with uppercase -> Don't modify
    else {
        name->data = "f_" + name->data;
    }
    /*
    if (pGen->undefined(name->data)) {
        throw_error(name->line, 4449, ("Undefined function " + name->data));
        pGen->namedef(name->data);
    }
    */

    out = genTemp(name);
    (*pGen) << out->data << " = " << name->data << "(" << exprs->data << ")" << std::endl;
    delete name; delete exprs;
}


// Binary operators
expr(A) ::= funcexpr(B).  { A = B; }
expr(A) ::= expr(B) PLUS        expr(C). { A = genTemp(C); (*pGen) << A->data << " = " << B->data << " + "  << C->data << std::endl; delete B; delete C; }
expr(A) ::= expr(B) MINUS       expr(C). { A = genTemp(C); (*pGen) << A->data << " = " << B->data << " - "  << C->data << std::endl; delete B; delete C; }
expr(A) ::= expr(B) MULTIPLY    expr(C). { A = genTemp(C); (*pGen) << A->data << " = " << B->data << " * "  << C->data << std::endl; delete B; delete C; }
expr(A) ::= expr(B) DIVIDE      expr(C). { A = genTemp(C); (*pGen) << A->data << " = " << B->data << " // " << C->data << std::endl; delete B; delete C; }
expr(A) ::= expr(B) MOD         expr(C). { A = genTemp(C); (*pGen) << A->data << " = " << B->data << " % "  << C->data << std::endl; delete B; delete C; }
expr(A) ::= expr(B) LSHIFT      expr(C). { A = genTemp(C); (*pGen) << A->data << " = " << B->data << " << " << C->data << std::endl; delete B; delete C; }
expr(A) ::= expr(B) RSHIFT      expr(C). { A = genTemp(C); (*pGen) << A->data << " = " << B->data << " >> " << C->data << std::endl; delete B; delete C; }
expr(A) ::= expr(B) BITAND      expr(C). { A = genTemp(C); (*pGen) << A->data << " = " << B->data << " & "  << C->data << std::endl; delete B; delete C; }
expr(A) ::= expr(B) BITOR       expr(C). { A = genTemp(C); (*pGen) << A->data << " = " << B->data << " | "  << C->data << std::endl; delete B; delete C; }
expr(A) ::= expr(B) BITXOR      expr(C). { A = genTemp(C); (*pGen) << A->data << " = " << B->data << " ^ "  << C->data << std::endl; delete B; delete C; }

// Unary operators
expr(A) ::= PLUS    expr(B). [UNARY] { A = genTemp(B); (*pGen) << A->data << " = +" << B->data << std::endl; delete B; }
expr(A) ::= MINUS   expr(B). [UNARY] { A = genTemp(B); (*pGen) << A->data << " = -" << B->data << std::endl; delete B; }
expr(A) ::= BITNOT  expr(B). [UNARY] { A = genTemp(B); (*pGen) << A->data << " = ~" << B->data << std::endl; delete B; }

// Logical operators
lexpr(A) ::= expr(B) EQ expr(C).   { A = genTemp(C); (*pGen) << A->data << " = (" << B->data << " == " << C->data << ")" << std::endl; delete B; delete C; }
lexpr(A) ::= expr(B) NE expr(C).   { A = genTemp(C); (*pGen) << A->data << " = (" << B->data << " != " << C->data << ")" << std::endl; delete B; delete C; }
lexpr(A) ::= expr(B) LE expr(C).   { A = genTemp(C); (*pGen) << A->data << " = (" << B->data << " <= " << C->data << ")" << std::endl; delete B; delete C; }
lexpr(A) ::= expr(B) LT expr(C).   { A = genTemp(C); (*pGen) << A->data << " = (" << B->data << " < "  << C->data << ")" << std::endl; delete B; delete C; }
lexpr(A) ::= expr(B) GE expr(C).   { A = genTemp(C); (*pGen) << A->data << " = (" << B->data << " >= " << C->data << ")" << std::endl; delete B; delete C; }
lexpr(A) ::= expr(B) GT expr(C).   { A = genTemp(C); (*pGen) << A->data << " = (" << B->data << " > "  << C->data << ")" << std::endl; delete B; delete C; }

lexpr(A) ::= lexpr(B) LAND lexpr(C).  { A = genTemp(C); (*pGen) << A->data << " = [" << B->data << ", " << C->data << "]" << std::endl; delete B; delete C; }
lexpr(A) ::= lexpr(B) LOR  lexpr(C).  { A = genTemp(C); (*pGen) << A->data << " = EUDOr([" << B->data << ", " << C->data << "])" << std::endl; delete B; delete C; }
lexpr(A) ::= LNOT lexpr(B). { A = genTemp(B); (*pGen) << A->data << " = EUDNot(" << B->data << ")" << std::endl; delete B; }
lexpr(A) ::= LNOT LNOT lexpr(B).  [DOUBLE_LNOT] { A = B; }


// Statements
vdef_stmt ::= VAR NAME(A) SEMICOLON. {
    (*pGen) << A->data << " = EUDVariable()" << std::endl;
    pGen->namedef(A->data);
    delete A;
}

vdefAssign_stmt ::= VAR NAME(A) ASSIGN expr(B) SEMICOLON. {
    (*pGen) << A->data << " = EUDVariable()" << std::endl;
    (*pGen) << A->data << " << " << B->data << std::endl;
    if(!pGen->namedef(A->data)) {
        throw_error(A->line, 196, ("Redeclaration of variable \'" + (A->data) + "\'"));
    }
    delete A;
}

vdefMulti_stmt ::= VAR nameList(A) SEMICOLON. {
    std::string& s = A->data;
    int varCount = std::count(s.begin(), s.end(), ',') + 1;
    (*pGen) << s << " = EUDCreateVariables(" << varCount << ")" << std::endl;

    // Register variables.
    commaListIter(s, [&](const std::string& varname) {
        if(!pGen->namedef(varname)) {
            throw_error(A->line, 196, ("Redeclaration of variable \'" + varname + "\'"));
        }
    });
    delete A;
}

vdefMultiAssign_stmt ::= VAR nameList(A) ASSIGN exprList(B) SEMICOLON. {
    std::string& s = A->data;
    int varCount = std::count(s.begin(), s.end(), ',') + 1;
    (*pGen) << s << " = EUDCreateVariables(" << varCount << ")" << std::endl;
    (*pGen) << "SetVariables([" << A->data << "], [" << B->data << "])" << std::endl;

    // Register variables.
    commaListIter(s, [&](const std::string& varname) {
        if(!pGen->namedef(varname)) {
            throw_error(A->line, 196, ("Redeclaration of variable \'" + varname + "\'"));
        }
    });

    delete A; delete B;
}

assign_stmt ::= NAME(A) ASSIGN expr(B) SEMICOLON. {
    (*pGen) << A->data << " << " << B->data << std::endl;
    if(pGen->undefined(A->data)) {
        throw_error(A->line, 103, ("Undeclared variable \'" + (A->data) + "\'"));
        pGen->namedef(A->data);
    }
    delete A; delete B;
}

assignMulti_stmt ::= nameList(A) ASSIGN exprList(B) SEMICOLON. {
    (*pGen) << "SetVariables([" << A->data << "], [" << B->data << "])" << std::endl;

    commaListIter(A->data, [&](const std::string& varname) {
        if(pGen->undefined(varname)) {
            throw_error(A->line, 103, ("Undeclared variable \'" + varname + "\'"));
            pGen->namedef(varname);
        }
    });

    delete A; delete B;
}

// Control blocks
stmtList ::= stmt.
stmtList ::= stmtList stmt.
block ::= LBRACKET stmtList RBRACKET.
block ::= LBRACKET error RBRACKET(A). {
    throw_error(A->line, 6974, "General syntax error");
}


// If~Else block
if_start(A) ::= IF(B). {
    A = genTemp(B);
    (*pGen) << A->data << " = Forward()" << std::endl;
}
if_header(C) ::= if_start(A) LPAREN lexpr(B) RPAREN. {
    (*pGen) << "EUDJumpIfNot(" << B->data << ", " << A->data << ")" << std::endl;
    C = A; delete B;
}

if_stmt ::= if_header(A) stmt. [IFX] {
    (*pGen) << A->data << " << NextTrigger()" << std::endl;
    delete A;
}

else_header(A) ::= if_header(B) stmt ELSE(C). [ELSE] {
    A = genTemp(C);
    (*pGen) << A->data << " = Forward()" << std::endl;
    (*pGen) << "EUDJump(" << A->data << ")" << std::endl;
    (*pGen) << B->data << " << NextTrigger()" << std::endl;
    delete B;
}

if_stmt ::= else_header(A) stmt. {
    (*pGen) << A->data << " << NextTrigger()" << std::endl;
    delete A;
}


// While block
while_start(A) ::= WHILE(B). {
    auto lbegin = genTemp(B);
    auto lend = genTemp(B);
    lblocks.push_back({lbegin->data, lbegin->data, lend->data});

    (*pGen) << lbegin->data << " = NextTrigger()" << std::endl;
    (*pGen) << lend->data << " = Forward()" << std::endl;
    A = lbegin;
    delete B;
}

while_cond ::= LPAREN lexpr(C) RPAREN(T). {
    auto lblock = getLastBlock();
    if(!lblock) {
        throw_error(T->line, 4217, "While scope is missing");
    }
    else {
        (*pGen) << "EUDJumpIfNot(" << C->data << ", " << lblock->lend << ")" << std::endl;
    }
    delete C; delete T;
}

while_stmt ::= while_start(T) while_cond stmt. {
    auto lblock = getLastBlock();
    if(!lblock) {
        throw_error(T->line, 4217, "While scope is missing");
    }
    else {
        (*pGen) << "EUDJump(" << lblock->lbegin << ")" << std::endl;
        (*pGen) << lblock->lend << " << NextTrigger()" << std::endl;
    }
    delete T;
}

// Loop exits
continue_stmt ::= CONTINUE SEMICOLON(T). {
    auto lblock = getLastBlock();
    if(!lblock) {
        throw_error(T->line, 1554, "Continue inside non-loop");
    }
    else {
        (*pGen) << "EUDJump(" << lblock->lcont << ")" << std::endl;
    }
    delete T;
}

break_stmt ::= BREAK SEMICOLON(T). {
    auto lblock = getLastBlock();
    if(!lblock) {
        throw_error(T->line, 5594, "Break inside non-loop");
    }
    else {
        (*pGen) << "EUDJump(" << lblock->lend << ")" << std::endl;
    }
    delete T;
}

// Function declaration
function_start(start) ::= FUNCTION NAME(name).  {
    // Preprocess name
    if(name->data[0] == '_' || ('A' <= name->data[0] && name->data[0] <= 'Z'));  // Name starts with uppercase -> Don't modify
    else {
        name->data = "f_" + name->data;
    }

    start = name;
    if(!pGen->namedef(name->data)) {
        throw_error(name->line, 4495, "Duplicate function name " + name->data);
    }
}

function_header ::= function_start(fname) LPAREN nameList(args) RPAREN. {
    (*pGen) << "@EUDFunc" << std::endl;
    (*pGen) << "def " << fname->data << "(" << args->data << "):" << std::endl;
    pGen->indent();

    // Register variables.
    const std::string& s = args->data;
    commaListIter(args->data, [&](const std::string& varname) {
        if(!pGen->namedef(varname)) {
            throw_error(args->line, 196, ("Redeclaration of variable \'" + varname + "\'"));
        }
    });

    tmpIndex = 1;
    delete fname; delete args;
}

function_header ::= function_start(fname) LPAREN RPAREN. {
    (*pGen) << "@EUDFunc" << std::endl;
    (*pGen) << "def " << fname->data << "():" << std::endl;
    pGen->indent();
    tmpIndex = 1;
    delete fname;
}

fdef_chunk ::= function_header stmt . {
    pGen->unindent();
}


// returns
return_stmt ::= RETURN emptyableExprList(exprs). {
    (*pGen) << "EUDReturn(" << exprs->data << ")" << std::endl;
}


// General statements
stmt ::= error SEMICOLON(A). { throw_error(A->line, 6974, "General syntax error"); }

stmt ::= SEMICOLON.
stmt ::= vdef_stmt.
stmt ::= vdefAssign_stmt.
stmt ::= vdefMultiAssign_stmt.
stmt ::= vdefMulti_stmt.
stmt ::= assign_stmt.
stmt ::= assignMulti_stmt.
stmt ::= expr SEMICOLON.
stmt ::= if_stmt.
stmt ::= while_stmt.
stmt ::= continue_stmt.
stmt ::= break_stmt.
stmt ::= return_stmt.
stmt ::= block.


// Program building block
chunk ::= fdef_chunk.
chunk ::= vdef_stmt.
chunk ::= vdefMulti_stmt.
chunks ::= .
chunks ::= chunks chunk.

%code {

// #define PARSER_DEBUG

std::string ParseString(const std::string& code)
{
    std::istringstream is(code);
    Tokenizer tok(is);
    void* pParser = ParseAlloc (malloc);
    PyGenerator pGen;
    Token* token;
    int line = 0;

    lblocks.clear();

#ifdef PARSER_DEBUG
    std::cout << "Parsing string [[[\n" << code << "\n]]]\n";
#endif

    tmpIndex = 1;
    errorOccured = false;
    while ((token = tok.getToken()) != nullptr) {
        if (line != tok.getCurrentLine()) {
            line = tok.getCurrentLine();
            pGen << "# (Line " << line << ") " << trim(tok.getCurrentLineString()) << std::endl;
        }
        if (tokenTypeConv(token)) {
            Parse (pParser, token->type, token, &pGen);
#ifdef PARSER_DEBUG
            ParseTrace(stdout, "-- ");
#endif
        }
        else {
            throw_error(token->line, 8104, "Invalid token");
        }
    }
    Parse (pParser, 0, nullptr, &pGen);
    ParseFree(pParser, free );

    if(!errorOccured) return pGen.str();
    // else throw std::runtime_error("Invalid syntax");
    return pGen.str();
}
}